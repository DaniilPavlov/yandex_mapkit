plugins {
    id "com.android.library"
}

def variant = project.hasProperty("yandexMapkit.variant") ? project.property("yandexMapkit.variant") : "lite"
def init = "new com.unact.yandexmapkit." + variant + ".Init" + variant.capitalize() + "()"

group 'com.unact.yandexmapkit'
version '1.0-SNAPSHOT'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.unact.yandexmapkit'
    }

    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 26
        buildConfigField "com.unact.yandexmapkit.Init", "YANDEX_MAPKIT_INIT", init
    }

    buildFeatures {
        buildConfig = true
    }

    sourceSets {
        main {
            java {
                include("com/unact/yandexmapkit/lite/*")

                if (variant == "lite") {
                    exclude("com/unact/yandexmapkit/full/*")
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    google()
     maven {url 'https://developer.huawei.com/repo/'}
}

dependencies {
    implementation 'com.yandex.android:maps.mobile:4.19.0-' + variant
}
